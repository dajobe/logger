#!/usr/bin/perl -w
#
# IRC chat logger
#
# $Source$
# $Id$
#
# (C) Copyright 2000 Dave Beckett, ILRT, University of Bristol
# http://www.ilrt.bristol.ac.uk/people/cmdjb/
#

use Net::IRC;
use File::Path;

my $irc = new Net::IRC;

my $Nick='logger';
my $Channel='#ilrt';
my $Server='my.desire.org';
my $Port='6667';
my $IRC_Name='Chat Logger';

my $Log_Root='/home/logger/chat-logs';

my $Max_Results=5;

######################################################################
%ENV=();
$ENV{PATH}='/bin:/usr/bin:/usr/local/bin';

my $host;
if(`uname -n` =~ /^([-\w.]+)$/) {
  $host=$1;
} else {
  $host='unknown';
}


my @Month_names = qw/XXX Jan Feb Mar Apr May Jun Jul Aug Sep Oct Nov Dec/;


my $conn = $irc->newconn(Nick    => $Nick,
			 Server  => $Server,
			 Port    => $Port,
			 Ircname => $IRC_Name,
			 LocalAddr => $host);


$::listening=1;
$::departing=0;


######################################################################
# Logging

sub log_event ($$) {
  my($t, $msg)=@_;

  return if !$::listening;

  my $c=$Channel;
  $c=~ s/\W//g;
  my $log_dir="$Log_Root/$Server:$Port/$c/";

  mkpath([$log_dir], 0, 0755) if ! -d $log_dir;
  # Failed!
  if (! -d $log_dir) {
    $conn->privmsg($Channel, "failed to create chat log dir, quitting");
    exit(0);
  }

  my @tm = localtime($t);
  $tm[5]+= 1900; $tm[4]++;
  my $date_time = sprintf("%04d-%02d-%02d %02d:%02d:%02d",
			  $tm[5], $tm[4], $tm[3],
			  $tm[2], $tm[1], $tm[0]);
  
  my $log_file = sprintf("$log_dir/%04d-%02d.log", $tm[5], $tm[4]);
  
  # Failed!
  if (!open(LOG, ">>$log_file")) {
    $conn->privmsg($Channel, "failed to append to log file, quitting - $!");
    exit(0);
  }
  
  print LOG "$date_time $msg\n";
  close(LOG);
}


sub get_logs () {
  my $c=$Channel;
  $c=~ s/\W//g;
  my $log_dir="$Log_Root/$Server:$Port/$c/";

  return () if !opendir(DIR, $log_dir);

  # In date order, newest lines at top
  my(@results)=();
  # Newest files first
  for my $file (reverse sort readdir(DIR)) {
    next unless $file =~ /^\d\d\d\d-\d\d.log/;
    open(LOG, "$log_dir/$file") or next;
    
    # Append to list, reverse date sorted list - i.e. older entries at end 
    my(@lines);
    while(<LOG>) {
      chomp;
      push(@lines, $_) if length $_;
    };
    close(LOG);
    push(@results, reverse @lines);
  }
  closedir(DIR);

  return(@results);
}


######################################################################
# Global events

# What to do when logger successfully connects.
sub on_connect {
  my ($self, $event) = @_;
  
  $self->join($Channel);
  $::listening=1;
  $conn->me($Channel, ' is listening');
}


# Prints the names of people in a channel when we enter.
sub on_names {
  my ($self, $event) = @_;
  my (@list, $channel) = ($event->args);
  
  # splice() only works on real arrays. Sigh.
  ($channel, @list) = splice @list, 2;
  
  log_event(time, "Users on $channel: @list");
}


# Reconnect to the server when we die.
sub on_disconnect {
  my ($self, $event) = @_;
  
  log_event(time, "Disconnected from ". $event->from(). " (". ($event->args())[0]. ")");

  return if $::departing;

  log_event(time, "Attempting to reconnect");
  $self->connect();
}


######################################################################
# Per connection events


# When received a private message
sub on_msg {
  my($self,$event)=@_;
  my $nick = $event->nick;
  
  my $arg = join(' ', $event->args);
  command_for_me($self, $nick, $arg, 1);
}


# What to do when we receive channel text.
sub on_public {
  my ($self, $event) = @_;
  my @to = $event->to;
  my ($nick, $mynick) = ($event->nick, $self->nick);
  my $arg = join(' ', $event->args);

  # Private stuff
  return if $arg =~ /^\[off\]/i;

  if ($arg =~ /^$mynick,\s+(.*)$/) {
    command_for_me($self, $nick, $1, 0);
  }

  log_event(time, "<$nick> $arg");
}


# What to do when someone leaves a channel logger is on.
sub on_part {
  my ($self, $event) = @_;
  my ($channel) = ($event->to)[0];
  
  log_event(time, sprintf("%s has left channel", $event->nick));
}


# What to do when someone joins a channel logger is on.
sub on_join {
  my ($self, $event) = @_;
  my ($channel) = ($event->to)[0];
  my $user_nick=$event->nick;

  log_event(time, sprintf("%s (%s) has joined channel", $user_nick, $event->userhost));

  my $output="Hello $user_nick, I may be recording you.  At present ";
  if(!$::listening) {
    $output.="I'm not listening.";
  } else {
    $output.="I'm listening.";
  }

  $conn->notice($event->nick, $output);
}


sub command_for_me ($$$$) {
  my($self, $from_nick, $command, $is_private)=@_;

  $command=~s/^\s+//;

  my $output='';

  my $dest_nick=($is_private ? $from_nick : $Channel);


  if($is_private && $command eq 'restart') {
    $::departing=1;
    $conn->me($Channel, ' is departing');
    $conn->quit;
    exec('/home/logger/logger');
  }
  
  if($command=~ /^(?:quit|finish|terminate|die die die)/i) {
    $::departing=1;
    $conn->me($Channel, ' is departing');
    $conn->quit;
    exit 0;
  }
  
  if($command=~ /^(?:be quiet|shut up|silence|sshush|stop)/i) {
    if($::listening) {
      $conn->me($Channel, ' is not listening');
      $::listening=0;
    }
    return;
  }
  
  if($command=~ /^(?:hello|listen|record|start|begin)/i) {
    if(!$::listening) {
      $conn->me($Channel, ' is listening');
      $::listening=1;
    }
    return;
  }

  if(!$::listening) {
    $output="I'm not listening. ";
  } else {
    $output="I'm listening. ";
  }

  # Allow question?
  if ($command =~ /^(.+)\?$/) {
    $command="grep $1";
  }

  if($command=~ /^help/i) {
    $output.=q{I understand 'quit', 'silence', 'listen', 'grep [-i] [max] <perl regex>'};
  }
  elsif ($command =~ /^(?:grep|search for|find)\s+(.+)$/) {
    my $search=$1;
    my $flags='';
    my $max=$Max_Results;

    if($search=~ s/^-i\s+//) {
      $flags='(?i)';
    }
    if($search=~ s/^(\d+)\s+(?:things about\s+|)//) {
      $max=$1;
    }

    my $orig_search=$search;

    # Only allow a few regexes

    # Remove (?...) blocks since they can do evals
    $search =~ s/\(\?[^)]+\)//g;
    # Remove variable references
    $search =~ s/\$//g;
    # Quote '/'s
    $search =~ s%/%\\/%g;


    # Prefix with flags
    $search=$flags.$search;

    my(@results);
    eval "\@results=grep(/$search/, get_logs())";
    if($!) {
      @results=();
      my $msg=$!; $msg=~ s/^ at .+$//;
      $output.=qq{ Sorry, search failed with error - $msg};
    } elsif (!@results) {
      $output.=qq{ Sorry, nothing found for '$orig_search'};
      $output.=qq{ (internally: "$search")} if $orig_search ne $search;
    } else {
      my $pl=(@results != 1) ? 's' : '';
      $output.=" I found ".scalar(@results)." answer$pl";
      if(@results > $max) {
	$output.=" (showing first $max)";
      }
      $output.="\n";
      $conn->notice($dest_nick, $output);

      for(my $i=0; ($i< @results) && ($i < $max); $i++) {
	$output="$i) ".$results[$i]."\n";
	$conn->notice($dest_nick, $output);
      }
      return;
    }
  } else {
    $output.="I don't understand that, $from_nick";
  }

  $conn->notice($dest_nick, $output);
}



# Install handlers
# On 'end of MOTD' event, join ilrt channel
$conn->add_global_handler(376, \&on_connect);
$conn->add_global_handler(353, \&on_names);
$conn->add_global_handler('disconnect', \&on_disconnect);

$conn->add_handler('msg', \&on_msg);
$conn->add_handler('public', \&on_public);
$conn->add_handler('part', \&on_part);
$conn->add_handler('join', \&on_join);
$conn->add_handler('kick', \&on_disconnect);


$irc->start;
